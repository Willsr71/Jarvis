buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "java"
    id "idea"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "ninja.miserable.blossom" version "1.0.1"
}

def buildNumber = System.env.BUILD_NUMBER ?: "SNAPSHOT"

def projectId = "jarvis"
project.group = "sr.will"
project.version = "1.0-" + buildNumber
project.description = "A bot for Discord"
def projectUrl = "https://github.com/Willsr71/Jarvis"
def projectAuthor = "Willsr71"
def projectSite = "https://will.sr"

def jarName = "${projectId}-${project.version.toString()}.jar"

repositories {
    jcenter()

    maven { url "https://repo.noxal.net/" }
}

dependencies {
    compile "net.noxal:common:1.5-112"
    compile "net.dv8tion:JDA:3.8.3_460"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "org.mariadb.jdbc:mariadb-java-client:2.4.0"
    compile "com.mashape.unirest:unirest-java:1.4.9"
    compile "com.google.guava:guava:25.1-jre"
}

jar {
    archiveName jarName
}

shadowJar {
    manifest {
        attributes "Main-Class": "sr.will.jarvis.Main"
    }

    archiveName jarName
}

task versionTxt() {
    doLast {
        new File("$projectDir/version.txt").text = "project.path=${project.group.replace('.', '/')}/${projectId}/${project.version}" +
                "\nproject.file=${projectId}-${project.version}"
    }
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter jar
}

blossom {
    def location = "src/main/java/sr/will/jarvis/Jarvis.java"

    replaceToken "@version@", project.version, location
}

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
    }

    repositories {
        jcenter()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://repo.noxal.net/" }
    }
}
